apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'maven-publish'

android {
    compileSdkVersion 28
    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 28
        versionCode = libProperties['VERSION_CODE']
        versionName libProperties['VERSION_NAME']
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation project(':core')

    implementation"org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    implementation "io.reactivex.rxjava2:rxjava:$rxJavaVersion"

    implementation 'com.google.android.gms:play-services-auth:16.0.1'

    androidTestImplementation "junit:junit:$junitVersion"
    androidTestImplementation "org.mockito:mockito-core:$mockitoVersion"
    androidTestImplementation "com.google.dexmaker:dexmaker:$dexmakerVersion"
    androidTestImplementation "com.google.dexmaker:dexmaker-mockito:$dexmakerVersion"
    androidTestImplementation "androidx.test:rules:$androidTestVersion"
}

ext {
    bintrayName = 'google'
    publishedArtifactId = 'google'

    libraryName = 'Rx Social Login'
    libraryDescription = 'Reactive social login Android library'
}

apply from: rootProject.file('gradle/bintray/install.gradle')
apply from: rootProject.file('gradle/bintray/bintray.gradle')

publishing {
    publications {
        library(MavenPublication) {
            setGroupId(libProperties['GROUP'])
            setArtifactId(bintrayName)
            version libProperties['VERSION_NAME']

            artifact(bundleReleaseAar)

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')
                configurations.implementation.allDependencies.each {
                    if (it.group != null && (it.name != null || !"unspecified".equals(it.name)) && it.version != null && !"unspecified".equals(it.version)) {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}